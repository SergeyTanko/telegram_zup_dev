

Функция ОбработатьВходящийЗапрос(Запрос) Экспорт
	
	ДанныеЗапроса = ДанныеЗапрос(Запрос);
	
	Если ДанныеЗапроса.Свойство("message") Тогда
		Возврат ОбработатьСообщение(ДанныеЗапроса.message);
	КонецЕсли;
	 
	Возврат Т_ОбщегоНазначенияHTTP.ПростойУспешныйОтвет();
	
		
КонецФункции

#Область Служебный
//////////////////////////////////////
Функция ДанныеЗапрос(Запрос)
	
	ТелоЗапроса = Запрос.ПОлучитьТелоКакСтроку();
	
	Возврат Т_ОбщегоНазначенияHTTP.ОбъектJSON(ТелоЗапроса)
	
КонецФункции

Функция ОбработатьСообщение(ДанныеСообщения)
	
	Если ДанныеСообщения.Свойство("from") Тогда
		ЗафиксироватьОтправителя(ДанныеСообщения.from);		
	КонецЕсли;
	
	ИдентификаторЧата = ДанныеСообщения.chat.id;
	
	Если ДанныеСообщения.Свойство("text") Тогда
		ТекстСообщения = ДанныеСообщения.text;
	Иначе 
		ТекстСообщения = "Пустое сообщение ... :(";
	КонецЕсли;
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("method","sendMessage");
	ДанныеОтвета.Вставить("chat_id",ИдентификаторЧата);
	ДанныеОтвета.Вставить("text",ТекстСообщения);
	
	Возврат Т_ОбщегоНазначенияHTTP.ОтветJSON(ДанныеОтвета);
	
КонецФункции

Процедура ЗафиксироватьОтправителя(ДанныеОтправителя)
	Идентификатор = ДанныеОтправителя.id;
	УчетнаяЗапись = Справочники.T_УчетныеЗаписиТелеграм.НайтиПоКоду(Идентификатор);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Возврат;
	КонецЕсли;
	
	ЧастиИмениПользователя = Новый Массив();
	
	Если ДанныеОтправителя.Свойство("first_name") Тогда
		ЧастиИмениПользователя.Добавить(ДанныеОтправителя.first_name);
	КонецЕсли;
	Если ДанныеОтправителя.Свойство("last_name") Тогда
		ЧастиИмениПользователя.Добавить(ДанныеОтправителя.last_name);
	КонецЕсли;
	Если ДанныеОтправителя.Свойство("user_name") Тогда
		ЧастиИмениПользователя.Добавить(ДанныеОтправителя.user_name);
	КонецЕсли;
	
	ИмяПользователя = СтрСоединить(ЧастиИмениПользователя," ");
	
	СпрОбъект 				= Справочники.T_УчетныеЗаписиТелеграм.СоздатьЭлемент();
	СпрОбъект.Код 			= Идентификатор;
	СпрОбъект.Наименование 	= ИмяПользователя;	
	СпрОбъект.Записать();
	
	
КонецПроцедуры
#КонецОбласти 
